plugins {
    id 'antlr'
    id 'java'
    id 'maven-publish'
}

group 'de.mlessmann'
version '1.1.1'

if (project.hasProperty("preRelease")) {
    apply from: './pre-release.gradle'
    def tag = project.ext.getPreReleaseTag()
    def i = tag.substring(tag.lastIndexOf('.') + 1)

    version "$version-rc.$i"


} else if (!project.hasProperty('release')) {
    version "$version-SNAPSHOT"
}

println "Using project version: $version"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

/// === Configuration === ///
project.ext {
    artifact = 'confort'
}

/// ===== Repositories / SourceSets / Dependencies ===== ///

repositories {
    maven {
        name = 'FearNixx'
        url uri('https://nexus.fearnixx.de/repository/maven-public')
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'build/generated-src/antlr/main']
        }
        antlr {
            srcDirs = ['src/main/antlr']
        }
    }
    lang {
        java {
            srcDirs = ['src/lang/java', 'build/generated-src/antlr/lang']
        }
        antlr {
            srcDirs = ['src/lang/antlr']
        }
    }
    api {
        java {
            srcDirs = ['src/api/java']
        }
    }
}

dependencies {
    antlr group: 'org.antlr', name: 'antlr4', version: '4.8'

    compile sourceSets.api.output
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    
    langCompile sourceSets.api.output
    langCompile sourceSets.main.output
    langCompile group: 'org.antlr', name: 'antlr4', version: '4.8'
    langCompile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    
    testCompile sourceSets.lang.output
    testCompile group: 'junit', name: 'junit', version: '4.13'
    
    // Check-against libraries
    testCompile group: 'org.json', name: 'json', version: '20201115'
    testCompile group: 'com.typesafe', name: 'config', version: '1.4.0'
}

/// ===== Tasks ===== ///

apply from: './antlr.gradle'
apply from: './artifacts.gradle'
apply from: './publication.gradle'

wrapper {
    gradleVersion = '5.4.1'
    distributionType = Wrapper.DistributionType.ALL
}