plugins {
    id 'antlr'
    id 'java'
    id 'maven-publish'
}

group 'de.mlessmann'
version '1.1.2'

if (project.hasProperty("preRelease")) {
    apply from: './pre-release.gradle'
    def tag = project.ext.getPreReleaseTag()
    def i = tag.substring(tag.lastIndexOf('.') + 1)

    version "$version-rc.$i"


} else if (!project.hasProperty('release')) {
    version "$version-SNAPSHOT"
}

println "Using project version: $version"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

/// === Configuration === ///
project.ext {
    artifact = 'confort'
}

/// ===== Repositories / SourceSets / Dependencies ===== ///

repositories {
    maven {
        name = 'FearNixx'
        url uri('https://nexus.fearnixx.de/repository/maven-public')
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'build/generated-src/antlr/main']
        }
        antlr {
            srcDirs = ['src/main/antlr']
        }
    }
    lang {
        java {
            srcDirs = ['src/lang/java', 'build/generated-src/antlr/lang']
        }
        antlr {
            srcDirs = ['src/lang/antlr']
        }
    }
    api {
        java {
            srcDirs = ['src/api/java']
        }
    }
}

dependencies {
    antlr 'org.antlr:antlr4:4.9.2'

    compile sourceSets.api.output
    compile 'org.slf4j:slf4j-api:1.7.32'
    
    langCompile sourceSets.api.output
    langCompile sourceSets.main.output
    langCompile 'org.antlr:antlr4:4.8'
    langCompile 'org.slf4j:slf4j-api:1.7.30'
    
    testCompile sourceSets.lang.output
    testCompile 'junit:junit:4.13.2'
    
    // Check-against libraries
    testCompile 'org.json:json:20190722'
    testCompile 'com.typesafe:config:1.4.0'
    testRuntime 'org.apache.logging.log4j:log4j-api:2.17.0'
    testRuntime 'org.apache.logging.log4j:log4j-core:2.17.0'
    testRuntime 'org.apache.logging.log4j:log4j-slf4j-impl:2.19.0'
}

/// ===== Tasks ===== ///

apply from: './antlr.gradle'
apply from: './artifacts.gradle'
apply from: './publication.gradle'

wrapper {
    gradleVersion '6.7.1'
    distributionType = Wrapper.DistributionType.ALL
}
