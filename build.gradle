plugins {
    id 'antlr'
    id 'java'
    id 'maven-publish'
}

group 'de.mlessmann'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8

/// ===== Repositories / SourceSets / Dependencies ===== ///

repositories {
    maven {
        name = 'FearNixx'
        url uri('https://nexus.fearnixx.de/repository/maven-public')
    }
}

sourceSets {
    api {
        java {
            srcDirs = ['src/api/java']
        }
    }
}

dependencies {
    antlr group: 'org.antlr', name: 'antlr4', version: '4.7.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

/// ===== Tasks ===== ///

/// ===== ANTLR 4 ===== ///

// BEGIN Fix for Gradle bug #2565
// "cannot find tokens"-bug with packaged grammar
// Found via. https://github.com/gradle/gradle/issues/2565#issuecomment-417916273
// From: https://github.com/apache/groovy/blob/master/subprojects/parser-antlr4/build.gradle#L34
final PARSER_PACKAGE_NAME = 'de.mlessmann.confort.antlr'
generateGrammarSource {
    outputs.cacheIf { true }
    arguments += ["-visitor", "-no-listener", "-package", PARSER_PACKAGE_NAME]

    doLast {
        def parserFilePattern = 'DeltaDescriptor*'
        def outputPath = generateGrammarSource.outputDirectory.canonicalPath
        def parserPackagePath = "${outputPath}/${PARSER_PACKAGE_NAME.replace('.', '/')}"
        file(parserPackagePath).mkdirs()
        copy {
            from outputPath
            into parserPackagePath
            include parserFilePattern
        }
        delete fileTree(outputPath) {
            include parserFilePattern
        }
    }
}
// END Fix #2565

/// ===== Generic gradle settings ===== ///

wrapper {
    gradleVersion = '4.2.1'
    distributionType = Wrapper.DistributionType.ALL
}