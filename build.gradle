plugins {
    id 'antlr'
    id 'java'
    id 'maven-publish'
}

group 'de.mlessmann'
def projectVersion = '1.0.0'

if (project.hasProperty("preRelease")) {
    apply from: './pre-release.gradle'
    def tag = project.ext.getPreReleaseTag()
    def i = tag.substring(tag.lastIndexOf('.') + 1)

    projectVersion += "-rc." + i


} else if (!project.hasProperty('release')) {
    projectVersion += '-SNAPSHOT'
}

println "Using project version: $projectVersion"
version projectVersion

sourceCompatibility = 1.8

/// === Configuration === ///
project.ext {
    artifact = 'confort'
    antlrVersion = '4.7.1'
}

/// ===== Repositories / SourceSets / Dependencies ===== ///

repositories {
    maven {
        name = 'FearNixx'
        url uri('https://nexus.fearnixx.de/repository/maven-public')
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'build/generated-src/antlr/main']
        }
        antlr {
            srcDirs = ['src/main/antlr']
        }
    }
    lang {
        java {
            srcDirs = ['src/lang/java', 'build/generated-src/antlr/lang']
        }
        antlr {
            srcDirs = ['src/lang/antlr']
        }
    }
    api {
        java {
            srcDirs = ['src/api/java']
        }
    }
}

dependencies {
    antlr group: 'org.antlr', name: 'antlr4', version: project.ext.antlrVersion

    compile sourceSets.api.output
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'
    
    langCompile sourceSets.api.output
    langCompile sourceSets.main.output
    langCompile group: 'org.antlr', name: 'antlr4', version: project.ext.antlrVersion
    langCompile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    
    testCompile sourceSets.lang.output
    testCompile group: 'junit', name: 'junit', version: '4.12'
    
    // Check-against libraries
    testCompile group: 'org.json', name: 'json', version: '20190722'
    testCompile group: 'com.typesafe', name: 'config', version: '1.3.4'
}

/// ===== Tasks ===== ///

apply from: './antlr.gradle'
apply from: './artifacts.gradle'
apply from: './publication.gradle'

wrapper {
    gradleVersion = '5.4.1'
    distributionType = Wrapper.DistributionType.ALL
}