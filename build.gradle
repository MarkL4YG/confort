plugins {
    id 'antlr'
    id 'java'
    id 'maven-publish'
}

group 'de.mlessmann'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8

/// === Configuration === ///
project.ext {
    antlrVersion = '4.7.1'
}

/// ===== Repositories / SourceSets / Dependencies ===== ///

repositories {
    maven {
        name = 'FearNixx'
        url uri('https://nexus.fearnixx.de/repository/maven-public')
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'build/generated-src/antlr/main']
        }
        antlr {
            srcDirs = ['src/main/antlr']
        }
    }
    lang {
        java {
            srcDirs = ['src/lang/java', 'build/generated-src/antlr/lang']
        }
        antlr {
            srcDirs = ['src/lang/antlr']
        }
    }
    api {
        java {
            srcDirs = ['src/api/java']
        }
    }
}

dependencies {
    antlr group: 'org.antlr', name: 'antlr4', version: project.ext.antlrVersion

    compile sourceSets.api.output
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    
    langCompile sourceSets.api.output
    langCompile sourceSets.main.output
    langCompile group: 'org.antlr', name: 'antlr4', version: project.ext.antlrVersion
    langCompile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    
    testCompile sourceSets.lang.output
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.json', name: 'json', version: '20180813'
}

/// ===== Tasks ===== ///

apply from: './buildLang.gradle'

compileJava() {
    dependsOn 'compileApiJava'
}

compileLangJava() {
    dependsOn 'compileJava'
}

task apiJar(type: Jar) {
    baseName = 'confort'
    version = project.version
    classifier = 'api'

    from sourceSets.api.allJava
    from sourceSets.api.output
    
    dependsOn 'compileApiJava'
}

task langJar(type: Jar) {
    baseName = 'confort'
    version = project.version
    classifier = 'langs'

    from sourceSets.lang.output

    dependsOn 'compileLangJava'
}

task runtimeJar(type: Jar) {
    baseName = 'confort'
    version = project.version
    classifier = 'runtime'

    from sourceSets.main.output
    
    dependsOn 'compileJava'
}

task allJar(type: Jar) {
    baseName = 'confort'
    version = project.version
    classifier = 'all'
    
    from sourceSets.api.output
    from sourceSets.main.output
    from sourceSets.lang.output
    
    dependsOn 'compileLangJava'
}

build {
    dependsOn 'apiJar'
    dependsOn 'langJar'
    dependsOn 'runtimeJar'
    dependsOn 'allJar'
}

wrapper {
    gradleVersion = '4.2.1'
    distributionType = Wrapper.DistributionType.ALL
}