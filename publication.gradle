publishing {
    repositories {
        mavenLocal()
        maven {
            def releaseRepository = 'https://nexus.fearnixx.de/repository/maven-releases'
            def snapshotRepository = 'https://nexus.fearnixx.de/repository/maven-snapshots'

            name = 'FNGNexus'
            url uri(project.version.endsWith('SNAPSHOT') ? snapshotRepository : releaseRepository)

            if (project.hasProperty('fngNexusUser') && project.hasProperty('fngNexusPass')) {
                credentials {
                    username "$fngNexusUser"
                    password "$fngNexusPass"
                }
            }
        }
    }

    publications {
        api(MavenPublication) {
            artifactId = "${project.ext.artifact}-api"
            artifact apiJar
            artifact apiSourcesJar {
                classifier "sources"
            }
        }
        lang(MavenPublication) {
            artifactId = "${project.ext.artifact}-lang"
            artifact langJar
            artifact langSourcesJar {
                classifier "sources"
            }
            
        }
        runtime(MavenPublication) {
            artifactId = "${project.ext.artifact}-runtime"
            artifact runtimeJar
            artifact runtimeSourcesJar {
                classifier "sources"
            }
        }
        all(MavenPublication) {
            artifactId = project.ext.artifact
            artifact allJar
            artifact allSourcesJar {
                classifier "sources"
            }
        }
    }
}